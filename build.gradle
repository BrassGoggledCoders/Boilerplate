buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

repositories {
    maven {
        name "K4Repo"
        url "http://maven.k-4u.nl"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

project.ext {
    buildNum = System.getenv("BUILD_NUMBER")
    mcVersion = "1.9"
    forgeVersion = "12.16.0.1811-1.9"
}

version = project.ext.mcVersion + "-8.0.0." + project.ext.buildNum

group= "brassgoggledcoders"
archivesBaseName = "boilerplate"

minecraft {
    version = "${project.ext.mcVersion}-${project.ext.forgeVersion}"

    runDir = "eclipse/"
    mappings = "snapshot_20160319"

    replace "@VERSION@", project.version
    replaceIn "ModInfo.java"

    replace "@VERSION@", project.version
    replaceIn "BoilerplateLib.java"
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    classifier = "universal"
}

task deobfJar(type: Jar) {
    from(sourceSets.main.output) {
        classifier = "deobf"
    }
}

task libJar(type: Jar) {
    from(sourceSets.main.output) {
        exclude("xyz/brassgoggledcoders/boilerplate/mod/**")
        exclude("xyz/brassgoggledcoders/boilerplate/api/**")
    }

    classifier = "lib"
}

task apiJar(type: Jar) {
    from(sourceSets.main.output) {
        include("xyz/brassgoggledcoders/boilerplate/api/**")
    }

    classifier = "api"
}

artifacts {
    archives deobfJar
    archives libJar
    archives apiJar
}

idea {
    module {
        inheritOutputDirs = true
    }
}

task("uploadJars", dependsOn: "reobf") {
    description = "uploads JARs"
    if (System.getenv("local_maven") != null) {
        apply plugin: 'maven'
        uploadArchives {
            repositories {
                mavenDeployer {
                    repository(url: "file://" + System.getenv("local_maven"))
                    pom {
                        groupId = project.group
                        version = project.version
                        artifactId = project.archivesBaseName
                        project {
                            name project.archivesBaseName
                            packaging 'jar'
                            description 'Boilerplate'
                            url ''
                            scm {
                                url 'https://github.com/BrassGoggledCoders/Boilerplate'
                                connection 'scm:git:git@github.com:BrassGoggledCoders/Boilerplate.git'
                                developerConnection 'scm:git:git@github.com:BrassGoggledCoders/Boilerplate.git'
                            }
                            issueManagement {
                                system 'github'
                                url 'https://github.com/BrassGoggledCoders/Boilerplate/issues'
                            }
                            licenses {
                                license {
                                    name 'MIT'
                                    url 'https://github.com/BrassGoggledCoders/Boilerplate/blob/master/LICENSE'
                                    distribution 'repo'
                                }
                            }
                            developers {
                                developer {
                                    id 'warlordjones'
                                    name 'warlordjones'
                                    roles { role 'developer' }
                                }
                                developer {
                                    id 'SkySom'
                                    name 'SkySom'
                                    roles { role 'developer' }
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}
